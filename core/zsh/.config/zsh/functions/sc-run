# vi: set ft=zsh ts=4 sw=4 sts=-1 sr noet si tw=0 fdm=manual:
local flag_fg=0 \
	flag_idle=0 \
	flag_root=0

while :; do
	case ${1-} in
		-h | --help)
			cat <<-'HELP'
				Flags:
					-f|--fg   : launch into foreground
					-i|--idle : idle priority
					-r|--root : launch as system service
			HELP
			return 0
			;;
		-f | --fg) flag_fg=1 ;;
		-i | --idle) flag_idle=1 ;;
		-r | --root) flag_root=1 ;;
		*)
			break
			;;
	esac
	shift
done

local -a cmd=(systemd-run --quiet --collect)

if ((!flag_root)); then
	cmd+=(--user)
fi
if ((flag_fg)); then
	cmd+=(--pty)
fi
if ((flag_idle)); then
	cmd+=(--nice=19 --property=CPUSchedulingPolicy=idle --property=IOSchedulingClass=idle)
fi

"${cmd[@]}" "$@"